grammar Hasdrubal ;

program     = { expr EOL } ;
expr        = definition ;
definition  = "let" NAME "=" expr [ "in" expr ]
            | "let" NAME "(" parameters ")" "=" expr
            | pipe
            ;
pipe        = func [ "|>" pipe ] ;
func        = "\" parameters "->" func
            | cond
            ;
cond        = "if" and "then" cond "else" cond
            | and
            ;
and         = or [ "and" and ] ;
or          = not [ "or" or ] ;
not         = "not" not
            | compare
            ;
compare     = add_sub_con [ ( ">" | "<" | ">=" | "<=" | "=" | "/=" ) compare ] ;
add_sub_con = mod_div_mul [ ( "<>" | "+" | "-" ) add_sub_con ] ;
mod_div_mul = exponent [ ( "*" | "/" | "%" ) mod_div_mul ] ;
exponent    = negate { "^" negate } ;
negate      = "-" negate
            | func_call
            ;
func_call   = list { "(" expr { "," expr } [ "," ] ")" } ;
list        = "[" [ expr [ "," expr ] [ "," ] ] "]"
            | tuple
            ;
tuple       = "(" [ expr { "," expr } [ "," ] ] ")"
            | scalar
            ;
scalar      = bool
            | FLOAT
            | INTEGER
            | STRING
            ;
bool        = "True"
            | "False"
            ;

parameters  = NAME { "," NAME } [ "," ] ;
              (* Function definitions must have at least 1 paramter. *)
